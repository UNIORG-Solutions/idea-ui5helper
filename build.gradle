buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "com.github.hurricup:gradle-grammar-kit-plugin:2017.1.1"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.2.17'
    id 'de.undercouch.download' version '3.2.0'
}

repositories {
    mavenCentral()
}


import de.undercouch.gradle.tasks.download.Download
import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

def isCi = System.env.CI != null
def ideVersion = "2017.3"

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'

allprojects {
    apply {
        plugin("idea")
        plugin("org.jetbrains.grammarkit")
        plugin("org.jetbrains.intellij")
    }

    repositories {
        mavenCentral()
    }

    idea {
        module {
            generatedSourceDirs.add(file('src/main/gen'))
        }
    }

    patchPluginXml {
        pluginId "${group}.${project.name}"

    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/gen'
        }
    }


    intellij {
        version null
        plugins 'JavaScriptLanguage', 'CSS'
        pluginName 'ui5-helper'
        instrumentCode false
        localPath PhpStormPath
        if (!isCi) {
            alternativeIdePath PhpStormPath
        }
    }

    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    dependencies {
        compileOnly("com.github.hurricup:gradle-grammar-kit-plugin:2017.1.1") {
            exclude group: 'org.jetbrains.plugins'
            exclude module: 'idea'
        }
        compile 'com.intellij:forms_rt:7.0.3'
        testCompile 'junit:junit:4.+'
    }

    grammarKit {
        grammarKitRelease = '1.5.2'
    }

}

task downloadPHPStorm(type: Download) {
    src "https://download.jetbrains.com/webide/PhpStorm-${ideVersion}.tar.gz"
    dest new File(project.projectDir, "debugger/lib/phpstorm-${ideVersion}.tar.gz")
    overwrite false
}

task unpackPHPStorm(dependsOn: downloadPHPStorm, type: Copy) {
    from tarTree("debugger/lib/phpstorm-${ideVersion}.tar.gz")
    into new File(project.projectDir, 'debugger/lib')
}

task generateBindingLexer(type: GenerateLexer) {
    source 'src/main/grammars/binding/BindingLexer.flex'
    targetDir 'src/main/gen/de/uniorg/ui5helper/binding/'
    targetClass 'BindingLexer'
}

task generateBindingParser(type: GenerateParser) {
    source 'src/main/grammars/binding/Binding.bnf'
    targetRoot 'src/main/gen'
    pathToParser '/de/uniorg/ui5helper/binding/RustParser.java'
    pathToPsiRoot '/de/uniorg/ui5helper/binding/psi/'
}

compileJava.dependsOn(generateBindingLexer, generateBindingParser, unpackPHPStorm)

group 'de.uniorg'
version '0.3.0-rc3'