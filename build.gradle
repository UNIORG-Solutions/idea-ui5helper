buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://dl.bintray.com/jetbrains/intellij-plugin-service" }
        maven { url = "https://jitpack.io" }
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.intellij' version '0.4.13'
    id "org.jetbrains.grammarkit" version "2019.2.2"
}

repositories {
    mavenCentral()
}

import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

def ideVersion = "2019.2.4"

def group  = 'de.uniorg'
def pluginVersion = '0.4.0'

allprojects {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    idea {
        module {
            generatedSourceDirs.add(file('src/main/gen'))
        }
    }

    patchPluginXml {
        version = pluginVersion
        pluginId  = "${group}.${project.name}"
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/gen'
        }
    }

    intellij {
        version ideVersion
        type 'IU'
        plugins 'JavaScriptLanguage', 'CSS'
        pluginName 'ui5helper'
        instrumentCode false
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    dependencies {
        compileOnly("com.github.hurricup:gradle-grammar-kit-plugin:2019.2.1") {
            exclude group: 'org.jetbrains.plugins'
            exclude module: 'idea'
        }
        compile 'com.intellij:forms_rt:7.0.3'
        testCompile 'junit:junit:4.+'
    }
}

task generateBindingLexer(type: GenerateLexer) {
    source = 'src/main/grammars/binding/BindingLexer.flex'
    targetDir = 'src/main/gen/de/uniorg/ui5helper/binding/'
    targetClass = 'BindingLexer'
}

task generateBindingParser(type: GenerateParser) {
    source = 'src/main/grammars/binding/Binding.bnf'
    targetRoot = 'src/main/gen'
    pathToParser = '/de/uniorg/ui5helper/binding/RustParser.java'
    pathToPsiRoot = '/de/uniorg/ui5helper/binding/psi/'
}

compileJava.dependsOn(generateBindingLexer, generateBindingParser)
