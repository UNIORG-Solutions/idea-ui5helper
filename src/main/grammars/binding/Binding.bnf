{
  parserClass="de.uniorg.ui5helper.binding.BindingParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix=""
  psiImplClassSuffix="Impl"
  psiPackage="de.uniorg.ui5helper.binding.psi"
  psiImplPackage="de.uniorg.ui5helper.binding.psi.impl"

  elementTypeHolderClass="de.uniorg.ui5helper.binding.psi.BindingTypes"
  elementTypeClass="de.uniorg.ui5helper.binding.psi.BindingElementType"
  tokenTypeClass="de.uniorg.ui5helper.binding.psi.BindingTokenType"

  psiImplUtilClass="de.uniorg.ui5helper.binding.psi.impl.BindingParserImplUtil"
}

value ::= item_*

private item_ ::= (binding_|STRING)
private binding_ ::= (simple_binding|complex_binding)
complex_array ::= BRACKET_OPEN complex_binding_value (COMMA complex_binding_value)* BRACKET_CLOSE
complex_object ::= CURLY_OPEN complex_binding_entry (COMMA complex_binding_entry)* CURLY_CLOSE
complex_boolean ::= TRUE|FALSE
complex_binding_value ::= QUOTED_STRING|complex_array|complex_object|NUMBER|complex_boolean
complex_binding_key   ::= STRING|QUOTED_STRING
complex_binding_entry ::= complex_binding_key COLON complex_binding_value
complex_binding ::= CURLY_OPEN complex_binding_entry (COMMA complex_binding_entry)*CURLY_CLOSE
simple_binding ::= CURLY_OPEN (STRING MODEL_SEP)? PATH_SEP? (STRING PATH_SEP)* STRING? CURLY_CLOSE